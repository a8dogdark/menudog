;
; SAVE #D:KEM2.ASM
;
KEMBUFFER =$B000
ROMRAM
	LDY #$00
KEMLOOP1
	LDA $e000,y
	STA KEMBUFFER,y
	LDA $e100,y
	STA KEMBUFFER+$100,y
	LDA $e200,y
	STA KEMBUFFER+$200,y
	LDA $e300,y
	STA KEMBUFFER+$300,y
	INY
	BNE KEMLOOP1
	LDA #>KEMBUFFER
	STA CHBAS
	STA CHBASE
	LDX #$C0
	SEI
	STY $D40E
	STX $CC
	STY $CB
LOOP
	LDA ($CB),Y
	DEC $D301
	STA ($CB),Y
	INC $D301
	INY
	BNE LOOP
	INC $CC
	BEQ @EXIT
	LDA $CC
	CMP #$D0
	BNE LOOP
	LDA #$D8
	STA $CC
	BNE LOOP
@EXIT
	LDA #$E0
	STA CHBAS
	STA CHBASE
	TYA	//y está en cero, según el código de arriba
KEMLOOP2
	STA KEMBUFFER,y
	STA KEMBUFFER+$100,y
	STA KEMBUFFER+$200,y
	STA KEMBUFFER+$300,y
	INY
	BNE KEMLOOP2
	DEC $D301
	LDX #$01
	LDY #$4C
	LDA #$13
	STX $EE17
	STY $ED8F
	STA $ED67
	LDX #$80
	LDY #$03
	STX $EBA3
	STY $EBA8
	LDY #$04
	LDA #$EA
NOP
	STA $ED85,Y
	DEY
	BPL NOP
	LDY #STACF-STACI
MOVE
	LDA STACI,Y
	STA $ECEF,Y
	DEY
	BPL MOVE
	LDA #$40
	STA NMIEN
	CLI
	CLC
	RTS
STACI
	LDA #$7D
	LDX $62
	BEQ CC0
	LDA #$64
CC0
	CLC
	ADC $EE19,X
	DEY
	BPL CC0
	CLC
	ADC $0312
	SEC
	SBC #$64
	BCC CC3
	STA $0312
	LDY #$02
	LDX #$06
	LDA #$4F
CC2
	ADC $0312
	BCC CC1
	INY
	CLC
CC1
	DEX
	BNE CC2
	STA $02EE
	STY $02EF
	JMP $ED96
CC3
	JMP $ED3D
STACF